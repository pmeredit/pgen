use std::str::FromStr;
use ast::*;

grammar;


match {
    //no idea why this regexp won't work
    //r#"('([^'\\]|\\.)*')|("([^"\\]|\\.)*")"# => STRING,
    r#"'([^'\\]|\\.)*'"#                       => SINGLESTRING,
    r#""([^"\\]|\\.)*""#  	               => DOUBLESTRING,
    r"-?[0-9]+"                                => INT,
    r"-?([0-9]*[.][0-9]+)"                     => FLOAT,
    r"\$\w+"                                   => COLUMN,
    r"[a-zA-Z_]\w*"                            => IDENTIFIER,
    "let","=", "in", "filter", "map",
    "zip", "defaults", "as", "where",
    "reduce", "start", "with", "using",
    "if", "then", "else",
    "switch", "=>", "default",
    "{","}","[","]", "(", ")",
    "+", "-", "*", "/", "%", ":", ";", ",", "**",
    "<", ">", "<=", ">=", "==", "!=",
    "||", "++", "@", "&&", "null", "true", "false"
}

pub Pipeline: Pipeline = PipelineItemList => Pipeline{stages: <>};

PipelineItemList = List<PipelineItem, ";">;

pub TopExpr: Box<Expr> =  { 
    "let" <a:Assignments> "in" <e:TopExpr> => 
 	Box::new(Expr::Let(
	    Box::new(Let{assignments: a, expr: e}))),
    "map" <i:OrExpr> <id:OptionalAs> InOrUsing <e:TopExpr> =>
        Box::new(Expr::Map(
	    Box::new(Map{input: i, ename: id, expr: e}))),
    "filter" <i:OrExpr> <id:OptionalAs> "where" <c:TopExpr> =>
        Box::new(Expr::Filter(
	    Box::new(Filter{input: i, ename: id, cond: c}))),
    "reduce" <i:OrExpr> "start" "with" <init:OrExpr> InOrUsing <e:TopExpr> =>
        Box::new(Expr::Reduce(
	    Box::new(Reduce{input: i, init: init, expr: e}))),
    "zip" <i:ArrayExpr> <d:OptionalDefault> =>
        Box::new(Expr::Zip(
	    Box::new(Zip{inputs: i, longest: match d { None => false, _=> true }, defaults: d}))),
    "if" <i:OrExpr> "then" <t:OrExpr> "else" <e:OrExpr> =>
     	Box::new(Expr::Cond(
	    Box::new(Cond{cond:i, then:t, otherwise:e}))),
    "switch" "{"  <c:Cases> "default" ":" <d:OrExpr> "}" =>
    	Box::new(Expr::Switch(
	    Box::new(Switch{cases:c, default:d}))),
    OrExpr,
};

InOrUsing: () = {
   "in"    => (),
   "using" => (),
};

OptionalAs: String = {
    ()           => "this".to_string(),
    "as" <id:ID> => id,
};

OptionalDefault: Option<Box<Expr>> = {
    ()                        => None,
    "defaults" <d:ArrayExpr>  => Some(d),
};

PipelineItem: PipelineItem = {
    <id:ID> ":" <o:TopExpr> => PipelineItem{stage_name: id, object: o}
};


Assignment: (String, Box<Expr>) = {
    <id:ID> "=" <val:TopExpr> => (id, val)
};

ObjItem: (String, Box<Expr>) = {
    <k:Str> ":" <v:TopExpr> => (k, v)
};

Case: (Box<Expr>, Box<Expr>) = {
    <c:OrExpr> "=>" <e:TopExpr> => (c, e)
};

List<T, Sep>: Vec<T> = { 
    <v:(<T> Sep)*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Assignments = List<Assignment, ",">;
Args        = List<TopExpr, ",">;
Cases       = List<Case, ",">;
ObjItems    = List<ObjItem, ",">;

Tier<Op,NextTier>: Box<Expr> = {
    Tier<Op,NextTier> Op NextTier => Box::new(Expr::Op(<>)),
    NextTier
};

OrExpr      = Tier<OrOp,      AndExpr     >;
AndExpr     = Tier<AndOp,     CompareExpr >;
CompareExpr = Tier<CompareOp, AddExpr     >;
AddExpr     = Tier<AddOp,     MulExpr     >;
MulExpr     = Tier<MulOp,     ExpExpr     >;
ExpExpr     = Tier<ExpOp,     BottomExpr  >;

OrOp: Opcode = {
    "||" => Opcode::Or,
};

AndOp: Opcode = {
    "&&"  => Opcode::And,
};

CompareOp: Opcode = {
    "==" => Opcode::Eq,
    "!=" => Opcode::Neq,
    "<=" => Opcode::Lte,
    ">=" => Opcode::Gte,
    "<"  => Opcode::Lt,
    ">"  => Opcode::Gt,
};

AddOp: Opcode = { 
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

MulOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
    "%" => Opcode::Mod,
};

ExpOp: Opcode = {
    "**" => Opcode::Exp,
    "++" => Opcode::StrCat,
    "@"  => Opcode::ArrCat,
};

BottomExpr: Box<Expr> = {
    "null" => Box::new(Expr::Null),
    Str    => Box::new(Expr::Str(<>)),
    Bool   => Box::new(Expr::Bool(<>)),
    Number => Box::new(Expr::Number(<>)),
    Float  => Box::new(Expr::Float(<>)),
    ID     => Box::new(Expr::ID(<>)),
    Col    => Box::new(Expr::Col(<>)),
    Object => Box::new(Expr::Object(<>)),
    <id:ID> "(" <args:Args> ")" => Box::new(Expr::App(id, args)),
    ArrayExpr,
    "(" <TopExpr> ")",
};

Object: Vec<(String, Box<Expr>)> = {
   "{" <ObjItems> "}",
};

ArrayExpr: Box<Expr> = {
   Array => Box::new(Expr::Array(<>)),
};

Array: Vec<Box<Expr>> = {
   "[" <Args> "]", 
};

Bool: bool = {
   "true" => true,
   "false" => false,
};

Str: String = {
     SINGLESTRING => String::from(&<>[1..<>.len()-1]),
     DOUBLESTRING => String::from(&<>[1..<>.len()-1]),
};

Number: i64 = {
     INT => i64::from_str(<>).unwrap(),
};

Float: f64 = {
     FLOAT => f64::from_str(<>).unwrap(),
};

Col: String = {
     COLUMN => <>.to_string(),
};

ID: String = {
     IDENTIFIER => <>.to_string(),
};
