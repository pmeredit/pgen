use std::str::FromStr;
use ast::{Expr, Opcode, Let};

grammar;

Assignments = List<Assignment, ",">;

List<T, Sep>: Vec<T> = { // (0)
    <v:(<T> Sep)*> <e:T?> => match e { // (1)
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Tier<Op,NextTier>: Box<Expr> = {
    Tier<Op,NextTier> Op NextTier => Box::new(Expr::Op(<>)),
    NextTier
};



pub Expr = { Tier<ExprOp, Factor>,
         "let" <a:Assignments> "in" <e:Expr> => 
	 	Box::new(Expr::Let(
		    Box::new(Let{assignments: a, expr: e}))) 
       };
Factor = Tier<FactorOp, Term>;

Assignment: (String, Box<Expr>) = {
    <id:ID> "=" <val:Expr> => (id, val)
};

ExprOp: Opcode = { // (3)
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

Term: Box<Expr> = {
    Num => Box::new(Expr::Number(<>)),
    ID => Box::new(Expr::ID(<>)),
    "(" <Expr> ")"
};

Num: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};

ID: String = {
    r"[a-zA-Z_]\w*" => String::from(<>)
};
